//===== rAthena Script =======================================
//= Broadcaster
//===== Author ===============================================
//= llchrisll
//===== Credits ==============================================
//= pAnZaA, GodLesZ,Jonne and other- Thanks guys for your help^^.
//===== Current Version ======================================
//= 1.4
//===== Changelog ============================================
//= 1.0 - Initial Release
//= 1.1 - Whisper Feature improved
//= 1.2 - Small change
//= 1.3 - Fixxed Problem about location > # instead of the : after Global/Map
//        Thanks to junjun7981 for the bugreport
//= 1.4 - Optimized for rAthena and bug fixes
//===== Compatible With ======================================
//= rAthena SQL 09/26-2015 Rev+
//===== Tested With ==========================================
//= rAthena SQL 09/26-2015 Revision
//===== Description ==========================================
//= Broadcasting via PM possible
//  How to do it explains the NPC In-Game
//===== Comments =============================================
//= ...
//============================================================
prontera.gat,164,168,3	script	Chat System	857,{

mes "[Broadcaster]";
mes "Hello "+strcharinfo(0);
mes "If you want to broadcast some messages,";
mes "type ^ff0000NPC:Broadcaster^000000 in the";
mes "Whisper Box, then type ^ff0000Global#<Text>^000000, or ^ff0000Map#<Text>^000000";
mes "in the Text Box.";
next;
mes "[Broadcaster]";
mes "Example:";
mes "[WhisperBox] NPC:Broadcaster";
mes "[TextBox] Global#Hi Guys!";
mes "";
mes "The message 'Hi Guys' will be";
mes "announced to everyone on the Server.";
mes "If I had put 'Map' instead of 'Global'";
mes "the message would be announced to the";
mes "current Map your on.";
next;
mes "[Broadcaster]";
mes "A Global Broadcast costs "+getvariableofnpc(.price[0],"Broadcaster")+" Zeny.";
mes "A Map Broadcast costs "+getvariableofnpc(.price[1],"Broadcaster")+" Zeny.";
close;
}

-	script	Broadcaster	-1,{

OnInit:
// Price > [0] == Global, [1] == Map
setarray .price[0],150000,75000;
end;

OnWhisperGlobal:
if(@whispervar0$ == "Global") {
	if(@whispervar1$ == "") {
		dispbottom strnpcinfo(0)+": You didn't put a message.";
		end;
	}
	if(Zeny < .price[0]) {
		dispbottom strnpcinfo(0)+": You don't have enough Zeny.";
		end;
	}
	set Zeny, Zeny - .price[0];
	announce "(From "+strcharinfo(0)+"): "+@whispervar1$+"",bc_yellow;

} else if(@whispervar0$ == "Map") {
	if(@whispervar1$ == "") {
		dispbottom strnpcinfo(0)+": You didn't put a message.";
		end;
	}
	getmapxy(.@mapn$,.@mapx,.@mapy,0);
	if(Zeny < .price[1]) {
		dispbottom strnpcinfo(0)+": You don't have enough Zeny.";
		end;
	}
	set Zeny, Zeny - .price[1];
	mapannounce .@mapn$,"(From "+strcharinfo(0)+"): "+@whispervar1$+"",bc_yellow;
	
} else
	dispbottom strnpcinfo(0)+": You didn't put a location, where to send the message.";
	
end;
}