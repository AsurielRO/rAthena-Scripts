//===== rAthena Script =======================================
//= Guild Ranking
//===== By ===================================================
//= llchrisll
//===== Version ==============================================
//= 1.0 - Initial Version
//		- Added many features (Didn't write that down...)
//		- Added the possiblity to use the donation via an Shop
//		  Items can only be set by the GM's, see 'OnInit'
//		- Fixxed some bugs with the Reward Function
//		  Reward can only be set by the GM's, see 'OnInit'
//= 1.1 - Changed Reward Giving via Quest, also fixxed some bugs reported by darkis
//		- Rewritten the Ranking Check, well it is using more memory now.... (Sorry for that)
//		- Added an Random EXP Boost depending how much "rank" points
//		  you possess, also added an menu option for it, see 'G_EXP'.
//		- Also rewritten the explanation how to add this system to WoE:SE 
//= 1.2 - Removed the Quest Area from preventing exploits
//= 1.3	- Removed Holiday List
//		- Removed Donation
//		- Script Optimization
//		- Removed `guild_rank_guild`
//		- Renamed `guild_rank_mem` to `guild_rank`
//====== ToDo List ===========================================
//= Ranking Decrease: How about when you lost a castle?
//					  Previous WoE: 3, This WoE: 2 > -1 Rank for Guild only?
//===== Tested With =========================================
//= rAthena 12/24/2017 Revision
//= GIT Hash:
//  ae69e506263f9b183c945f49b1d88c5fc2b73a6a
//===== Description ==========================================
//= This Script handles a Ranking System in the Guild itself,
//= the ranking itself can increase and decrease.
//= Increasing the Ranking:
//	- Emperium Break during WoE
//
//= Decreasing of the Ranking:
//  - Killing the Guild Members in PvP Rooms
//
//= Also you can recieve other rewards from breaking the Emp,
//	which is done by collecting points.
//===== MySQL Entries =======================================
/* //= Member Table:
DROP TABLE IF EXISTS `guild_rank`;
CREATE TABLE `guild_rank` (
  `char_id` int(10) unsigned NOT NULL,
  `guild_id` smallint(5) unsigned NOT NULL,
  `rank` smallint(5) unsigned DEFAULT '0',
  `woe` smallint(5) unsigned DEFAULT '0',
  PRIMARY KEY (`char_id`)
) ENGINE=MyISAM;
*/
//===== Comments =============================================
//= You need to edit the following line in
//  npc/guild/agit_main.txt
//========== Find this ==========//
/*
// Adjust Economy Invest Level for Castle
set .@Economy,GetCastleData(strnpcinfo(2),2) - 5;
if (.@Economy < 0) set .@Economy, 0;

//========== Add above ==========//
// Callfunc for Guild Rank System
	callfunc "GLD_RANK_WOE";
	
//= WoE:SE : Open these file under npc/guild2/
  arug_cas02.txt  arug_cas04.txt  schg_cas01.txt  schg_cas03.txt  schg_cas05.txt
  arug_cas01.txt  arug_cas03.txt  arug_cas05.txt  schg_cas02.txt  schg_cas04.txt

//========== Find this, which only shows up once every file. =========//
mapannounce "arug_cas01","The emperium has been shattered!",bc_map,"0x00FF00",FW_NORMAL,20,0,40; 
// ------- or -------- //
announce "The emperium has been shattered!",bc_map,"0x00FF00",FW_NORMAL,20,0,40;

//========== Add after ==========//
// Callfunc for Guild Rank System
	callfunc "GLD_RANK_WOE";
*/
//============================================================
-	script	GuildRanks#Events	-1,{

OnInit:
query_sql "SHOW TABLES LIKE 'guild_rank'",.@gld_tbl$;
// If the table doesn't exists
if(.@gld_tbl$ == "") 
	set $gld_ranker_tbl,0;
else 
	set $gld_ranker_tbl,1;
// Checking if the System is on or not
if(!$gld_ranker_tbl) { 
	disablenpc "GuildRanks#Events";
	end;
}
setarray .expboost[0],5,10,15,20,25; // Exp Boost in %
setarray .rankreq[0],15,30,60,120,240; // Rank Points required
// Example: To gain 15% EXP Bonus, you need 60 Rank Points.
set .boostuse,1; // Which day it will be activated on: 0-6 = Sunday - Saturday, 7 = Random
switch(.boostuse) { 
	case 0: set .expdayn$,"Sunday"; break;
	case 1: set .expdayn$,"Monday"; break;
	case 2: set .expdayn$,"Tuesday"; break;
	case 3: set .expdayn$,"Wednesday"; break;
	case 4: set .expdayn$,"Thursday"; break;
	case 5: set .expdayn$,"Friday"; break;
	case 6: set .expdayn$,"Saturday"; break;
	default: set .expdayn$,"a random day."; break;
}	
set .GldEXPBoost,0;

if(gettime(4) == ( (.boostuse != 7) ? .boostuse : rand(7) ) ) {
	set .GldEXPBoost,1;
	announce "Guild Rank System: EXP Bonus has been activated.",0,0x00FF00;
}
end;

OnNPCKillEvent:
if(!getcharid(2)) end; // If the player is not in a guild.
// =========== EXP Boost ============== //
if(!.GldEXPBoost) end;
if(EXPLastRank < query_sql("SELECT `rank` FROM `guild_rank` WHERE `guild_id` = '"+getcharid(2)+"' AND `char_id` = '"+getcharid(0)+"'",.@rank) ) {
	set EXPLastRank,.@rank;
	for( set .@b,0; .@b < getarraysize(.rankreq); set .@b,.@b + 1)
		if(EXPLastRank >= .rankreq[.@b])
			if(.rankreq[.@b+1] != 0) {
				set EXPBoost,.expboost[.@b];
				if(EXPLastRank < .rankreq[.@b+1] ) break;
			} else {
				set EXPBoost,.expboost[.@b];
				break;
			}
}
set .@bexp,(getmonsterinfo(killedrid,3)*EXPBoost);
set .@jexp,(getmonsterinfo(killedrid,4)*EXPBoost);
if(getgmlevel()) dispbottom "Debug: Monster: "+getmonsterinfo(killedrid,0)+" with "+.@bexp+" Base EXP, "+.@jexp+" Job EXP";
set BaseEXP,BaseEXP + .@bexp;
set JobEXP,JobEXP + .@jexp;
dispbottom "EXP Bonus: "+EXPBoost+"%. You have recieved additionally "+.@bexp+" Base EXP and "+.@jexp+" Job EXP.";
end;

OnPCKillEvent:
if(!getcharid(2)) end;
if(getmapflag(strcharinfo(3),mf_pvp) == 0) end;

query_sql "SELECT `name` FROM `guild_member` WHERE `guild_id` = '"+getcharid(2)+"'",.@gld_mem$;
for( set .@g,0; .@g < getarraysize(.@gld_mem$); set .@g,.@g + 1) {
	if(rid2name(killerrid) == .@gld_mem$[.@g]) {
		query_sql "UPDATE `guild_rank` SET `rank` = `rank` - 1 WHERE `guild_id` = '"+getcharid(2)+"' AND `char_id` = '"+killerrid+"'";
		break;
	}
}
end;

OnPCLoginEvent:
if($gld_ranker_tbl) announce "The Guild Ranking System is online.",4,0x00FF00;
if(.GldEXPBoost == 1) announce "Guild Rank System: EXP Bonus has been activated.",4,0x00FF00;;
if(!getcharid(2)) end;
if(EXPBoost) set EXPBoost,0;
end;

// ========================= Auto Guild Checking =========================//
// This part checks the Registered Guilds and their members.
// If the guild doesn't exists anymore or an Member has left a guild,
// they will be deleted from the ranking and if the new Guild is registered
// it will insert them into the new guild.
// ==========================================================================
OnClock0000:
set .GldEXPBoost,0;
if(gettime(4) == ( (.boostuse != 7) ? .boostuse : rand(7) ) ) {
	set .GldEXPBoost,1;
	announce "Guild Rank System: EXP Bonus has been activated.",0,0x00FF00;;
}
OnRankCheck:
if(query_sql("SHOW TABLES LIKE 'guild_rank'",.@g_tb) == 0) end;
// Which Guild is registered and still exists
query_sql "SELECT `guild_id` FROM `guild_rank`",.@gld;
for( set .@w,0; .@w < getarraysize(.@gld); set .@w,.@w + 1) {
  	if(query_sql("SELECT `guild_id` FROM `guild` WHERE `guild_id` = '"+.@gld[.@w]+"'",.@c_gld) != 0) {
	
// Getting the Guild Member ID's from the Guild_Member Table
		query_sql "SELECT `char_id` FROM `guild_member` WHERE `guild_id` = '"+.@gld[.@w]+"'",.@gld_mem;

		for ( set .@m,0; .@m < getarraysize(.@gld_mem); set .@m,.@m+1) {
		// Checking if the Member is in the ranking
			if(query_sql("SELECT `char_id` FROM `guild_rank` WHERE `guild_id` = '"+.@gld[.@w]+"' AND `char_id` = '"+.@gld_mem[.@m]+"'",.@c_chid) == 0) {
			
				// Checking in which Guild the Member is
				if(query_sql("SELECT `guild_id` FROM `guild_member` WHERE `char_id` = '"+.@gld_mem[.@m]+"'",.@c_gid) != .@gld[.@w])
				
				// If the Member doesn't exists anymore in this guild
					query_sql "DELETE FROM `guild_rank` WHERE `char_id` = '"+.@gld_mem[.@m]+"'";
				
				// Member is not in the ranking
				query_sql "INSERT INTO `guild_rank` ( `guild_id` , `char_id` ) VALUES ( '"+.@gld[.@w]+"' , '"+.@gld_mem[.@m]+"' )";
			}
		}
// If the Guild doesn't exist anymore delete it from the ranking.
	} else {
		query_sql "DELETE FROM `guild_rank` WHERE `guild_id` = '"+.@gld[.@w]+"'";
		announce "The guild "+getguildname(.@gld[.@w])+" has been disbanded.",0,0xFF0000;
	}
}
end;
}

//OnAgitBreak:
function	script	GLD_RANK_WOE	{
query_sql "UPDATE `guild_rank` SET `rank` = `rank` + 1 , `woe` = `woe` + 1 WHERE `guild_id` = '"+getcharid(2)+"' AND `char_id` = '"+getcharid(0)+"'";
announce "You have gained 1 Point for breaking an Emperium while WoE.",4,0xFF0000;
return;
}

// ===================== Guild Shop ==================== //
-	shop	GUILD_SHOP	-1,512:-1

// ===================== Guild Rank Manager ====================== //
prontera,163,169,4	script	Guild Ranker#gldrank	110,{

mes .n$;
// **********************************************************************//
//					     // GM Menu Selection \\					 	 //
// **********************************************************************//	
if(getgmlevel() >= 80) {
	mes "Player or GM Menu?";
	if(select("- Player Menu:- GM Menu") - 1) goto G_GM;
	next;
	mes .n$;
}
// **********************************************************************//
//						 // No MySQL Entries made \\				 	 //
// **********************************************************************//	
if(!$gld_ranker_tbl) {
	mes "I'm sorry, but the System hasn't been enabled yet.";
	close;
}
// **********************************************************************//
//						  // Not in a Guild \\					 	 //
// **********************************************************************//	
if(!getcharid(2)) {
	mes "I'm sorry, but you are not in a guild.";
	close;
}
// **********************************************************************//
//					  		// Main Greeting \\						 	 //
// **********************************************************************//	
mes "Hello, "+strcharinfo(0)+"!";
if(query_sql("SELECT `guild_id` FROM `guild_rank` WHERE `guild_id` = '"+getcharid(2)+"'",@gld_check) == 0) {
	mes "As I can see your Guild has not been registered yet,";
	if(strcharinfo(0) == getguildmaster(getcharid(2))) {
		mes "wanna register it now?";
		mes "Note: The Guild Ranking only benefits WoE-based Guilds.";
		if(select("- Yes register:- No, thanks") - 1) close;
		query_sql "SELECT `char_id` FROM `guild_member` WHERE `guild_id` = '"+getcharid(2)+"' ORDER BY `char_id` ASC",.@char_id;
		for( set .@g,0; .@g < getarraysize(.@char_id); set .@g,.@g + 1)
			query_sql "INSERT INTO `guild_rank` ( `char_id` , `guild_id` , `rank` , `woe` ) VALUES ( '"+.@char_id[.@g]+"' , '"+getcharid(2)+"' , '0' , '0' )";
		close;
	} else {
		mes "if you want to join, please inform your Guild Master about it.";
		close;
	}
}
mes "How can I help ya?";
next;
switch(select("- EXP Boost Info:- See Ranking:- Get Reward:- Guild Shop:- Nothing")) {

// **********************************************************************//
//							// EXP Boost Info \\					 	 //
// **********************************************************************//		

	case 1:
	mes .n$;
	mes "The EXP Boost is global to every guild and depends on the guild's rank.";
	mes "It will be activated on "+getvariableofnpc(.expdayn$,"GuildRanks#Events");
	mes "Status: " + ( (getvariableofnpc(.GldEXPBoost,"GuildRanks#Events") == 1) ? "Active" : "Not Active.");
	mes " ";
	mes "Current Rank Requirements and EXP Boost.";
	for( set .@e,0; .@e < getarraysize(getvariableofnpc(.expboost,"GuildRanks#Events")); set .@e,.@e + 1) 
		mes getvariableofnpc(.rankreq[.@e],"GuildRanks#Events")+" Points = "+getvariableofnpc(.expboost[.@e],"GuildRanks#Events")+"% EXP Boost.";
	break;
	
// **********************************************************************//
//								// Ranking \\						 	 //
// **********************************************************************//	
	
	case 2:
	mes .n$;
	mes "What kind of ranking do you want to see?";
	next;
	switch(select("- Emperium Breaks:- Member Ranking")) {
	
// **********************************************************************//
//							 // Emperium Breaks \\					 	 //
// **********************************************************************//	

		case 1:
		mes .n$;
		query_sql "SELECT `char_id` , `woe` FROM `guild_rank` WHERE `guild_id` = '"+getcharid(2)+"' ORDER BY `woe` DESC",.@char_id,.@woe;
		query_sql "SELECT count( `woe` ) FROM `guild_rank` WHERE `guild_id` = '"+getcharid(2)+"'",.@woe2;
		mes "Total Emp Breaks:";
		mes ""+.@woe2;
		mes " ";
		mes "Name - Break Quantity";
		for( set .@g,0; .@g < getarraysize(.@char_id); set .@g,.@g + 1) {
			query_sql "SELECT `name`  FROM `guild_member` WHERE `guild_id` = '"+getcharid(2)+"' AND `char_id` = '"+.@char_id[.@g]+"'",.@c_name$;
			mes .@c_name$+" - "+.@woe[.@g];
		}
		break;

// **********************************************************************//
//							  // Member Ranking \\					 	 //
// **********************************************************************//	
		
		case 2:
		mes .n$;
		query_sql "SELECT `char_id` , `rank` FROM `guild_rank` WHERE `guild_id` = '"+getcharid(2)+"' ORDER BY `rank` DESC",.@char_id,.@rank;
		mes "Name - Ranking";
		for( set .@gm,0; .@gm < getarraysize(.@char_id); set .@gm,.@gm + 1) {
			query_sql "SELECT `name`  FROM `guild_member` WHERE `guild_id` = '"+getcharid(2)+"' AND `char_id` = '"+.@char_id[.@gm]+"'",.@c_name$;
			mes .@c_name$+" - "+.@rank[.@gm];
		}
		break;
	}
	break;
	
// **********************************************************************//
//							  // Recieving Reward \\				 	 //
// **********************************************************************//			
	case 3:
	query_sql "SELECT `woe` FROM `guild_rank` WHERE `guild_id` = '"+getcharid(2)+"' AND `char_id` = '"+getcharid(0)+"'",.@woe;
	mes .n$;
	mes "Your current balance is:";
	mes "WoE Points: "+.@woe;
	next;
	mes .n$;
	mes "Reward - Points needed"; 
	for( set .@r,0; .@r < getarraysize(.rew_woe); set .@r,.@r + 1) {
		mes .rew_woe_am[.@r]+"x "+( (.rew_woe[.@r] == 1)?"Zeny": getitemname(.rew_woe[.@r]) + " (ID: " + .rew_woe[.@r] + ")" ) + " - "+.rew_woe_need[.@r];
		set .@r_menu$,.@r_menu$ + "- "+( (.rew_woe[.@r] == 1)?"Zeny": getitemname(.rew_woe[.@r])) + ( (.rew_woe[.@r+1] != 0)?":":"");
	}
	mes " ";
	mes "Please choose the reward you want.";
	next;
	set .@m,select(.@r_menu$) - 1;

	mes .n$;
	mes "You have chosen:";
	mes .rew_woe_am[.@m] + "x " + ( (.rew_woe[.@m] == 1) ? " Zeny": getitemname(.rew_woe[.@m]) + " (ID: " + .rew_woe[.@m] + ")" ) + " - "+.rew_woe_need[.@m];
	mes "Is that correct?";
	next;
	if(select("- Yes:- No") - 1) close;
	mes .n$;
	if(.rew_woe_need[.@m] > .@woe) {
		mes "I'm sorry, but it seems you don't have enough points for this reward.";
		close;
	} 
	if(.rew_woe[.@m] == 1) {
		mes "You have recieved "+.@rew_woe_am[.@m]+" Zeny.";
		set Zeny,Zeny + .rew_woe_am[.@m];
	} else {
		mes "You have recieved "+.rew_woe_am[.@m] + "x "+getitemname(.rew_woe[.@m]);
		getitem .rew_woe[.@m],.rew_woe_am[.@m];
	}
	query_sql "UPDATE `guild_rank` SET `woe` = `woe` - "+.rew_woe_need[.@m]+" WHERE `guild_id` = '"+getcharid(2)+"' AND `char_id` = '"+getcharid(0)+"'";
	break;

// **********************************************************************//
//					     // Guild Shop \\			 					 //
// **********************************************************************//		
	case 4:
	mes .n$;
	mes "Hello Master, how can I help you?";
	next;
	if(select("- Open Shop:- Nothing") - 1) close;

	mes .n$;
	mes "I will now open the shop for you.";
	mes "Note: The Skill Discount won't work here.";
	mes "The difference will be shown, but I will use the actual costs.";
	close2;
	callshop "GUILD_SHOP",1;
	npcshopattach "GUILD_SHOP";
	end;

	OnBuyItem:
	for( set @bid,0; @bid < getarraysize(@bought_nameid); set @bid,@bid + 1) {
		for( set @sid,0; @sid < getarraysize(.gshop_id); set @sid,@sid + 1) {
			if(.gshop_id[@sid] == @bought_nameid[@bid]) {
				set @cost_item,(.gshop_cost[@sid]*@bought_quantity[@bid]);
				set @cost_total,(@cost_total+@cost_item);
				break;
			}
		}
	}
	if(@cost_total > Zeny) {
		dispbottom "You don't have enough Zeny.";
		end;
	}
	for( set .@b,0; .@b < getarraysize(@bought_nameid); set .@b,.@b + 1) 
		getitem @bought_nameid[.@b],@bought_quantity[.@b];
	
	dispbottom "Thank you for shopping.";
	set Zeny,Zeny - @cost_total;
	set @cost_total,0;
	deletearray @bought_nameid[0],128;
	deletearray @bought_quantity[0],128;
	end;
	
	case 6:
	break;
}
close;
// **********************************************************************//
//								  // GM Menu \\						 	 //
// **********************************************************************//	

G_GM:
next;
mes .n$;
mes "Hello "+strcharinfo(0)+",";
if($gld_ranker_tbl) {
	mes "how can I help you?";
	next;
	switch(select("- Guild Ranks:- Re-Check Rank List:- Delete Tables")) {
		
		case 1:
		query_sql "SELECT `guild_id` , `rank` , `woe` FROM `guild_rank` WHERE `rank` > '0' ORDER BY `rank` DESC",.@r_gld,.@r_rank,.@r_woe;
		mes .n$;
		mes "Only Guilds with Ranking Points will be displayed!";
		if(.@r_gld[0] == 0) {
			mes "There is no guild to display.";
			close;
		}
		mes "The format of the list will be like this:";
		mes " ";
		mes " Guild Name: Points";
		mes "===== Emp Breaks: ===";
		mes "x";
		mes "*******************";
		for( set @r,0; @r < getarraysize(.@r_gld); set @r,@r + 1) {
			next;
			mes getguildname(.@r_gld[@r])+": "+.@r_rank;
			mes "===== Emp Breaks: ===";
			mes .@r_woe[@r];
			mes "*******************";
		}
		break;
		
		case 2:
		mes .n$;
		mes "I will now re-check the Ranking Status for all guilds.";
		doevent "GuildRanks#Events::OnRankCheck";
		break;
		
		case 3:
		mes .n$;
		mes "Are you sure to delete everything?";
		if(select("- Delete:- Nevermind") - 1) close;
		query_sql "DROP TABLE IF EXISTS `guild_rank`";
		disablenpc "GuildRanks#Events";
		announce "The Guild Ranking System is offline.",0,0xFF0000;
		set $gld_ranker_tbl,0;
		end;
	}
	
} else {
	mes "I'm sorry, but the ranking has not been activated yet,";
	mes "but it's also necessary to install the required entries in your database.";
	next;
	mes .n$;
	mes "First I will check if you installed it through the entries at the top of the script file.";
	mes "Then I will activate it.";
	mes "If that is not the case, I will install and activate the system for you if you want.";
	next;
	query_sql "SHOW TABLES LIKE 'guild_rank'",.@c_tbl$;
	if(.@c_tbl$[0] == "") {
		mes .n$;
		mes "Result:";
		mes "The entries are not yet installed, wanna install them?";
		next;
		if(select("- Install it:- No") - 1) {
			mes .n$;
			mes "Maybe next time.";
			close;
		}
		mes .n$;
		mes "Here we go";
		//= Member Table:
		query_sql "CREATE TABLE `guild_rank` ( `char_id` int(10) , `guild_id` smallint(5) , `rank` smallint(5) , `woe` smallint(5) )";
		enablenpc "GuildRanks#Events";
		announce "The Guild Ranking System is online.",0,0x00FF00;
		set $gld_ranker_tbl,1;

	} else {
		mes .n$;
		mes "It seems like the entries has been installed, then I will activate the system if you want.";
		if(select("- Please proceed~:- Stop please") - 1) close;
		enablenpc "GuildRanks#Events";
		announce "The Guild Ranking System is online.",0,0x00FF00;
		set $gld_ranker_tbl,1;
	}
}
close;

OnInit:
set .n$,"[Guild Ranker]";
// Guild Rewards:
// .rew_woe: Item ID, when you put 1 it will give Zeny as Reward.
// .rew_woe_am: Amount of the Items or Zeny.
// .rew_woe_need: The points needed
// For Emp Breaking
setarray .rew_woe[0],1,969;
setarray .rew_woe_am[0],50000,10;
setarray .rew_woe_need[0],1,5;
setarray .gshop_id[0],7619,7620;
setarray .gshop_cost[0],200000,200000;
// Attaching Items To the Shop
npcshopdelitem "GUILD_SHOP",512; 
for ( set .@s,0; .@s < getarraysize(.gshop_id); set .@s,.@s + 1 ) 
	npcshopadditem "GUILD_SHOP",.gshop_id[.@s],.gshop_cost[.@s];
end;
}