//===== rAthena Script =======================================
//= GM Activity Log
//===== By ===================================================
//= llchrisll
//===== Version ==============================================
//= 1.0 - Initial Version
//      - Added "Edit GM List" - Part
//      - Made some improvements about the Log Timer
//      - Forgot to change it back to the correct version
//= 1.1 - Script Optimization
//===== Tested With =========================================
//= rAthena SQL 07/16-2017 Revision
//===== Description ==========================================
//= This Scripts allows you to log the activity,
//  how many days, weeks, months and years he was online.
//===== Comments =============================================
//= None yet..
//============================================================
-	script	GMLog	-1,{

OnPCLoginEvent:
if(getgmlevel() < 1) end;
if(gettimetick(2) >= #gm_time && ($gm_logger == 1)) {
	query_sql "SELECT `acc_id`,`gm_name`,`day`,`week`,`month`,`year` FROM `gm_log` WHERE `acc_id` = '"+getcharid(3)+"'",.@acc_id,.@gm_name$,.@day,.@week,.@month,.@year;
	if(!.@acc_id) {
		query_sql "INSERT INTO `gm_log` ( `acc_id` , `gm_name` , `day` , `week` , `month` , `year` ) VALUES (  '"+getcharid(3)+"' , '"+escape_sql(strcharinfo(0))+"' , '1' , '0' , '0' , '0' )";
		set .@day,1;
		set #gm_time,gettimetick(2) + 86400;
		announce "[Server]: Your Activity Status has been updated to "+.@day+" day(s), "+.@week+" week(s), "+.@month+" month(s), "+.@year+" year(s).",bc_self;
		sleep2 2000;
		announce "[Server]: See ya tomorrow =3.",bc_self;
		end;
	}
	if(.@acc_id == getcharid(3) && .@gm_name$ != strcharinfo(0)) 
		query_sql "UPDATE `gm_log` SET `gm_name` = '"+escape_sql(strcharinfo(0))+"' WHERE `acc_id` = '"+getcharid(3)+"'";
	set .@day,.@day + 1;
	if(.@day > 7) {set .@day,0; set .@week,.@week + 1;}
	if(.@week > 4) {set .@week,0; set .@month,.@month + 1;}
	if(.@month > 12) {set .@month,0; set .@year,.@year + 1;}
	announce "[Server]: Your Activity Status has been updated to "+.@day+" day(s), "+.@week+" week(s), "+.@month+" month(s), "+.@year+" year(s).",bc_self;
	sleep2 2000;
	announce "[Server]: See ya tomorrow!",bc_self;
	query_sql "UPDATE `gm_log` SET `day` = '"+.@day+"' , `week` = '"+.@week +"' , `month` = '"+.@month+"' , `year` = '"+.@year+"' WHERE `acc_id` = '"+getcharid(3)+"'";
	set #gm_time,gettimetick(2) + 86400;		

} else if(!#gm_time && $gm_logger == 1) {
	query_sql "INSERT INTO `gm_log` ( `acc_id` , `gm_name` , `day` , `week` , `month` , `year` ) VALUES (  '"+getcharid(3)+"' , '"+escape_sql(strcharinfo(0))+"' , '1' , '0' , '0' , '0' )";
	set #gm_time,gettimetick(2) + 86400;
	
} else if(#gm_time > 1 && $gm_logger == 0) {
	announce "[Server]: The GM Logging System is momentally offline.",bc_self;
	set #gm_time,1;
} else if(#gm_time == 1 && $gm_logger == 1) {
	announce "[Server]: The GM Logging System is back online.",bc_self;
	set #gm_time,gettimetick(2) + 86400;
}
end;
}
prontera.gat,147,172,4	script	GM Activity Log	833,{
set .n$,"[GM Activity Log]";

mes .n$;
if(getgmlevel() >= 80) {
	if($gm_table == 1) {
		mes "Hello, GM.";
		mes "What do ya wanna do?";
		next;
		switch(select("- Show Log Entries:- En/Disable the System:- Edit GM List:- Delete MySQL Table")) {
			
			case 1:
			query_sql "SELECT `gm_name`,`acc_id`,`day`,`week`,`month`,`year` FROM `gm_log` ORDER BY `acc_id` ASC",.@gm_name$,.@acc_id,.@day,.@week,.@month,.@year;
			mes .n$;
			mes "I will list now the Entries:";
			if(.@gm_name$ == "") {
				mes " ";
				mes "There are no Entries";
				mes "in the Log.";
				close;
			}
			for( set .@o,0; .@o < getarraysize(.@acc_id); set .@o,.@o + 1) {
				next;
				mes "^FF0000"+.@gm_name$[.@o]+"^000000:";
				mes "-----------------";
				mes .@day[.@o]+" Day's";
				mes .@week[.@o]+" Week's";
				mes .@month[.@o]+" Month's";
				mes .@year[.@o]+" Year's";
				mes "======================";
			}
			break;
			
			case 2:
			mes .n$;
			mes "Do you want to";
			mes (!$gm_logger) ? "enable" : "disable";
			mes "GM Logging System?";
			if(select("- "+((!$gm_logger) ? "^00BB22Enable^000000" : "^FF2200Disable^000000")+" it:- Nothing") - 1) break;
			next;
			mes .n$;
			if($gm_logger == 0) {
				mes "I enabled the GM Logging System.";
				set $gm_logger,1;
				
			} else if($gm_logger == 1) {
				mes "I disabled the GM Logging System.";
				set $gm_logger,0;
			}
			break;
			
			case 3:
			mes .n$;
			mes "I will now list the GM's.";
			query_sql "SELECT `gm_name`,`acc_id`,`day`,`week`,`month`,`year` FROM `gm_log` ORDER BY `acc_id` ASC",.@gm_name$,.@acc_id,.@day,.@week,.@month,.@year;
			mes " ";
			if(.@gm_name$ == "") {
				mes "There is no GM";
				mes "in the Log.";
				mes " ";
			}
			for( set .@o,0; .@o < getarraysize(.@acc_id); set .@o,.@o + 1) {
				mes "^FF0000"+.@gm_name$[.@o]+"^000000:";
				mes "-----------------";
				set .@gm_list$,.@gm_list$ + "- "+.@gm_name$[.@o]+ ( (.@gm_name$[.@o+1] != "")?":":"");
			}
			mes "What do you want to do?";
			next;
			switch(select("- Add:- Edit:- Remove")) {
					
				case 1:
				mes .n$;
				mes "Now type the name of the GM you want to add.";
				next;
				input .@new_gm$;
				mes .n$;
				for( set .@g,0; .@g < getarraysize(.@gm_name$); set .@g,.@g + 1) 
					if(.@new_gm$ == .@gm_name$[.@g]) {	mes "This GM is already in the list."; close; }
					
				query_sql "SELECT `account_id` FROM `char` WHERE `name` = '"+@new_gm$+"'",.@gm_aid;
				if(!.@gm_aid) { mes "This character doesn't exists, try again please."; close; }
				query_sql "SELECT `level` FROM `login` WHERE `account_id` = '"+.@gm_aid+"'",.@gm_lvl;
				if(!.@gm_lvl) { mes "This isn't a GM, please try again."; close;	}
				query_sql "INSERT INTO `gm_log` ( `acc_id` , `gm_name` , `day` , `week` , `month` , `year` ) VALUES (  '"+.@gm_aid+"' , '"+@new_gm$+"' , '1' , '0' , '0' , '0' )";
				mes "The GM " + .@new_gm$+" has been added to the Log.";
				break;
					
				case 2:
				mes .n$;
				mes "Which GM do you want to edit?";
				set .@l,select(.@gm_list$) - 1;
				next;
				mes .n$;
				mes "You have chosen "+.@gm_name$[.@l]+", what now?";
				next;
				if(select("- Reset Activity Timers:- Nothing") - 1) close;
				mes .n$;
				mes "The times of the GM "+.@gm_name$[.@l]+" has been reseted.";
				close2;
				query_sql "UPDATE `gm_log` SET `day` = '0' , `week` = '0' , `month` = '0' , `year` = '0' WHERE `acc_id` = '"+getcharid(3,.@gm_name$[.@l])+"'";
				set #gm_time,0;
				break;
			
				case 3:
				mes .n$;
				mes "Which GM do you want to remove?";
				set .@d,select(.@gm_list$) - 1;
				next;
				mes "You have chosen " + .@gm_name$[.@d];
				mes "Really want to delete this GM from the Logs?";
				if(select("- No, stop:- Yes, please") - 1) close;
				mes .n$;
				query_sql "DELETE FROM `gm_log` WHERE `gm_name` = '"+escape_sql(.@gm_name$[.@d])+"'";
				mes "The GM "+.@gm_name$[.@d];
				mes "has been deleted from the Logs.";
				break;
			}
			break;
				
			case 4:
			mes .n$;
			mes "Do you really want to delete the MySQL Tables?";
			if(select("- Yes, I wanna:- No, don't!!!") - 1) close;
			next;
			query_sql "DROP TABLE `gm_log`";
			set $gm_logger,0;
			set $gm_table,0;
			mes .n$;
			mes "The MySQL Tables got deleted and the System automatically deactivated.";
			break;
		}
		
	} else if(!$gm_table) {
		mes "There is no Table for the GM Log System in your MySQL Database.";
		if(select("- Please create one:- Bye") - 1) close;
		next;
		query_sql "CREATE TABLE IF NOT EXISTS `gm_log` ( `gm_name` CHAR( 255 ) , `acc_id` INT( 10 ) , `day` INT( 8 ) , `week` INT( 8 ) , `month` INT( 8 ) , `year` INT( 8 ))";
		set $gm_table,1;
		set $gm_logger,1;
		mes .n$;
		mes "The Tables were created";
		mes "and the System automatically";
		mes "activated.";
		close;
	}
	
} else if(getgmlevel() >= 1 && getgmlevel() < 80) {
	mes "Wanna see your";
	mes "Online Activity?";
	if(select("- No, leave:- Yes, please") - 1) close;
	query_sql "SELECT `day`,`week`,`month`,`year` FROM `gm_log` WHERE `acc_id` = '"+getcharid(3)+"'",.@day,.@week,.@month,.@year;
	if(!(.@day && .@week && .@month && .@year)) {
		next;
		mes "I'm sorry, but";
		mes "there isn't";
		mes "any Log of you.";
		close;
	}
	mes " ";
	mes .@day+" Day's";
	mes .@week+" Week's";
	mes .@month+" Month's";
	mes .@year+" Year's";
	close;
} else {
	mes "I'm sorry, but";
	mes "I can't help you.";
	close;
}
end;
}