//===== rAthena Script =======================================
//= Personal Shops
//===== Author ===============================================
//= llchrisll
//===== Credits ==============================================
//= Soloa for permission to script his project and for the idea.
//===== Current Version ======================================
//= 1.0
//===== Changelog ============================================
//= 1.0 - Initial Release
//===== Compatible With ======================================
//= rAthena SQL 09/26-2015 Revision and later
//===== Tested With ==========================================
//= rAthena SQL 09/26-2015 Revision
//===== Description ==========================================
//= ...
//===== ToDO =============================================
//= Testing
//============================================================
/*
CREATE TABLE IF NOT EXISTS `shop_owner` (
 `shop_id` int(10) unsigned NOT NULL,
 `acc_id` int(10) unsigned NOT NULL,
 `shop_name` varchar(255) default 'Unknown Shop',
 `char_name` varchar(255) default 'Unknown Character',
 `cur_type` tinyint(3) unsigned default '1',
 `cur_ix` tinyint(3) unsigned default '0',
 `balance` int(12) unsigned default '0',
 `sales` int(12) unsigned default '0',
 `timer` int(12) unsigned default '0',
 PRIMARY KEY ( `shop_id` )
 ) ENGINE=MyISAM;
 
 CREATE TABLE IF NOT EXISTS `shop_content` (
 `shop_id` int(10) unsigned NOT NULL,
 `cont_id` int(10) unsigned NOT NULL,
 `cont_price` int(10) unsigned NOT NULL,
 `cont_stock` int(10) unsigned default '1'
 ) ENGINE=MyISAM;
 */
prontera,150,176,4	script	Shop Rental	826,{

mes .n$;
mes "Welcome "+strcharinfo(0)+"!";
if(query_sql("SHOW TABLES LIKE 'shop_owner'",.@sh_tbl) == -1) { // Checking if SQL Tables are installed
	mes "My Services are currently not available.";
	mes "Please come back another time.";
	close;
}
mes "How can I help you?";
next;
switch(select("- Renting/Dissolving Shop:- Information:- Shop Management:- Shop Area:- Nevermind")) {

	case 1:
	mes .n$;
	query_sql "SELECT `shop_id` FROM `shop_owner` WHERE `acc_id` = '"+getcharid(3)+"'",.@sh_id;
	if(.@sh_id) {
		mes "Do you really want to dissolve your shop?";
		mes "Every Item in your shop will put back in your inventory.";
		mes "Note: You need to have enough space to accept your items or I can't dissolve your shop as long there are items.";
		mes "Note 2: You also have to recieve your saved balance first, before you can completely dissolve your shop.";
		if(select("- Yes:- No") - 1) close;
		next;
		mes .n$;
		query_sql "SELECT `cont_id` , `cont_stock` FROM `shop_content` WHERE `shop_id` = '"+.@sh_id+"'",.@c_id,.@c_am;
		for ( set .@c,0; .@c < getarraysize(.@c_id); set .@c,.@c + 1) {
			if(!.@c_am[.@c]) {
				query_sql "DELETE FROM `shop_content` WHERE `shop_id` = '"+.@sh_id+"' AND `cont_id` = '"+.@c_id[.@c]+"'";
				continue;
			}
			if(checkweight(.@c_id[.@c],.@c_am[.@c]) == 1) {
				getitem .@c_id[.@c],.@c_am[.@c];
				query_sql "DELETE FROM `shop_content` WHERE `shop_id` = '"+.@sh_id+"' AND `cont_id` = '"+.@c_id[.@c]+"'";
			} else
				set .@i,.@i + 1;
		}
		if(.@i) {
			mes .n$;
			mes "I'm sorry, but I couldn't give you "+.@i+" items back, since your don't have the required space for.";
			mes "Please repeat this, as soon you have enough space for more items.";
			close;
		}
		query_sql "SELECT `balance` FROM `shop_owner` WHERE `shop_id` = '"+.@sh_id+"'",.@blc;
		if(.@blc) {
			mes "You didn't recieve your saved balance yet, please do this first.";
			close;
		}
		mes "The requirement to dissolve the shop has been reached.";
		mes "I will now dissolve your shop.";
		next;
		mes .n$;
		query_sql "DELETE FROM `shop_owner` WHERE `shop_id` = '"+.@sh_id+"'";
		query_sql "DELETE FROM `shop_content` WHERE `shop_id` = '"+.@sh_id+"'";
		disablenpc "Shop Spot "+.@sh_id+"#"+.@sh_id;
		donpcevent "Shop Spot "+.@sh_id+"#"+.@sh_id+"::OnShopReLoad";
		mes "Your shop has been succesfully dissolved.";
		mes "If you want to rent an shop again just talk to me when I have a free spot available.";
		close;
	}
	query_sql "SELECT `shop_id` FROM `shop_owner`",.@sh_sel;
	if(getarraysize(.@sh_sel) >= .sh_spot) {
		mes "I'm sorry, but there are no free spots, please come back as soon there is a free spot.";
		close;
	}
	mes "For you to be able to rent an shop, I have to charge an fee of "+.rent_pr+"x "+ ( (.rent_type == 1)?"Zeny": ( (.rent_type == 2)?"Cash Points":getitemname(.rent_type)))+".";
	mes "So do you want to rent one?";
	if(select("- Yes:- No") - 1) close;
	next;
	mes .n$;
	if(.rent_type == 1 && Zeny < .rent_pr) {
		mes "You don't have the required amount of Zeny.";
		close;
	} else if(.rent_type == 2 && #CASHPOINTS < .rent_pr) {
		mes "You don't have the required amount of Cash Points";
		close;
	} else if(.rent_type == 3 && countitem(.rent_item) < .rent_pr) {
		mes "You don't have the required amount of "+getitemname(.rent_item)+".";
		close;
	}
	for ( set .@s,1; .@s < getarraysize(.@sh_sel); set .@s,.@s + 1) 
		if(.@sh_sel[.@s-1] != (.@s)) break;
		
	set .@sh_new,.@s; // New Shop ID
	mes "First I need to ask you to give the following information:";
	mes " > Shop Name";
	mes " > Currency";
	next;
	mes .n$;
	mes "Shop Name:";
	input(.@sh_n$);
	mes "\""+.@sh_n$+"\"";
	mes " ";
	mes "Currency:";
	mes "- Use the number in front!!";
	mes " 1 = Zeny";
	mes " 2 = Cash Points or Kafra Points";
	mes " 3 = Custom Points";
	mes " Note: For 2 and 3 a list will be displayed after choosing.";
	mes " 4 = Items";
	if(input(.@cur_type,1,4) != 0) {
		mes .n$;
		mes "Invalid Currency, try again.";
		close;
	}
	next;
	mes .n$;
	if(.@cur_type == 2 || .@cur_type == 3) {
		mes "Please choose which points you want to use:";
		if(.@cur_type == 2) 
			for ( set .@c,0; .@c < getarraysize(.c_cash_disp$); set .@c,.@c + 1)
				set .@c_menu$,.@c_menu$ + "- "+.c_cash_disp$[.@c]+ ( (.c_cash_disp$[.@c] != "")?":":"");
		else if(.@cur_type == 3)
			for ( set .@c,0; .@c < getarraysize(.c_char_disp$); set .@c,.@c + 1)
				set .@c_menu$,.@c_menu$ + "- "+.c_char_disp$[.@c]+ ( (.c_char_disp$[.@c] != "")?":":"");
			
		set .@c_sel,select(.@c_menu$) - 1;
		next;
		mes .n$;
		mes "Chosen Points:";
		mes ( (.@cur_type == 2)?.c_cash_disp$[.@c_sel]:.c_char_disp$[.@c_sel]);
		mes " ";
		mes "Is this correct?";
		if(select("- Yes:- No") - 1) close;
		next;
		mes .n$;
	} else if(.@cur_type == 4) {
		mes "Please type the Item ID of the Item you want to use as currency:";
		next;
		input(.@c_sel);
		mes .n$;
		if(getitemname(.@c_sel) == "" || getitemname(.@c_sel) == "null") {
			mes "I'm sorry, but you have put an invalid Item ID.";
			close;
		}
		mes "Chosen Item: "+getitemname(.@c_sel);
		mes " ";
		mes "Is this correct?";
		if(select("- Yes:- No") - 1) close;
		next;
		mes .n$;
	}
	mes "You have succesfully rented an shop.";
	mes "Please visit your spot, your Shop ID is \""+.@sh_new+"\" or look for your Shop Name.";
	query_sql "INSERT INTO `shop_owner` ( `shop_id` , `acc_id` , `shop_name` , `char_name` , `cur_type` , `cur_ix` ) VALUES ( '"+.@sh_new+"' , '"+getcharid(3)+"' , '"+escape_sql(.@sh_n$)+"' , '"+escape_sql(strcharinfo(0))+"' , '"+.@cur_type+"' , '"+.@c_sel+"' )";
	switch(.rent_type) {
		case 1: set Zeny,Zeny - .rent_pr; break;
		case 2:	set #CASHPOINTS,#CASHPOINTS - .rent_pr;	break;
		default: delitem .rent_type,.rent_pr; break;		
	}
	donpcevent "Shop Spot "+.@sh_new+"#"+.@sh_new+"::OnShopReLoad";
	break;

	// Information
	case 2:
	mes .n$;
	mes "Here are some informations about the Rentable Shops:";
	mes " ";
	mes "[ Rental Prices ]";
	mes "To be able to get an spot for your own personal Shop, you need to pay an rental fee of "+.rent_pr+"x "+( (.rent_type == 1)?"Zeny":( (.rent_type == 2)?"Cash Points":getitemname(.rent_type)) )+".";
	mes "Afterwards I will need the following from you; Shop Name and currency to be used, which are listed below.";
	mes " ";
	mes "[ Shop Management ]";
	mes "You can manage your shop directly via me by checking your balance; adding, editing and deleting an Item from the Shop.";
	mes "Exchanging an Item is not possible, you have to delete one first before adding the new one later.";
	mes "Also if one item already exists in your shop, you can't add it again.";
	mes " ";
	mes "[ - Shop Currencies ]";
	mes "There currently the following currencies possible:";
	mes " > \"Zeny\"";
	mes " > \"Cash Points\"/\"Kafra Points\"";
	mes " > \"Event Points\" or anything like that.";
	mes " > \"Items\"";
	mes " ";
	mes "[ - Price Limits ]";
	mes "For every currency is also an Limit so you can't overcharge:";
	mes " > Zeny: "+.zeny_pr_max;
	mes " > Cash Points/Kafra Points: "+.var_pr_max;
	mes " > Items: "+.item_pr_max;
	mes " ";
	mes "[ - Max Stock Amount ]";
	mes "One of the last things is the Maximum Stock Amount.";
	mes "This means you can only have up to "+.item_am_max+" of one Item in your shop stock.";
	mes " ";
	mes "[ Spot Unlocking ]";
	mes "Last but and least is about the Shop Spots themself.";
	mes "There are total of "+.sh_sp_total+" possible.";
	if(.sh_sales) {
		query_sql "SELECT SUM( `sales` ) FROM `shop_owner`",.@sales;
		mes "But to keep the market clean, there "+( (.@sales/.sh_sales < 1)?"is":"are" )+" currently "+( (.@sales/.sh_sales < 1)?"1":(.@sales/.sh_sales) )+" Shop Spots available.";
		mes "Every "+.sh_sales+" item sold (each Item is counted) an new Spot opens, which will be checked every 12 hours.";
	} else 
		mes "Which are all available, if not taken already.";
	mes " ";
	mes "This should be everything, if you want to ask again about something, I'll gladly help you out.";
	break;
	
	// Shop Management
	case 3:
	mes .n$;
	query_sql "SELECT `shop_id` FROM `shop_owner` WHERE `acc_id` = '"+getcharid(3)+"'",.@sh_id;
	if(!.@sh_id) {
		mes "You have to rent an shop first, before you can manage one.";
		close;
	}
	query_sql "SELECT `cont_id` , `cont_price` , `cont_stock` FROM `shop_content` WHERE `shop_id` = '"+.@sh_id+"'",.@c_id,.@c_price,.@c_am;
	if(getarraysize(.@c_id) > 0 ) {
		mes "Shop Content:";
		mes "> Item Name (Item ID) - Item Stock - Item Price";
		for ( set .@e,0; .@e < getarraysize(.@c_am); set .@e,.@e + 1) {
			mes "> "+ getitemname(.@c_id[.@e])+" ("+.@c_id[.@e]+") - "+.@c_am[.@e]+"x - "+.@c_price[.@e];
			if(!.@c_am[.@e]) // Checking if Item Stock is empty and setting .@r variable to +1
				set .@r,.@r + 1;
		}
		mes " ";
		if(.@r) 
			mes "Your Shop currently has "+.@r+" Item Stocks emptied, please refill them or change the Item itself.";
	}
	mes "What do you want to do next?";
	next;
	query_sql "SELECT `shop_name` , `balance` , `cur_type` , `cur_ix` FROM `shop_owner` WHERE `acc_id` = '"+getcharid(3)+"'",.@sh_n$,.@bal,.@cur_type,.@cur_ix;
	switch(select("- Check Balance:- Change Shop Name:- Change Currency:- Add Item:- Edit Item:- Delete Item:- Leave")) {
		
		// Check Balance
		case 1:
		mes .n$;
		mes "Checking your balance right now.....";
		mes "Your Balance is "+.@bal+"x "+ ( (.@cur_type == 1)?"Zeny":( (.@cur_type == 2)?.c_cash_disp$[.@cur_ix]: ( (.@cur_type == 3)?.c_char_disp$[.@cur_ix]:getitemname(.@cur_ix) ) ) );
		if(.@bal) {
			mes "I will give it to you now.";
			next;
			switch(.@cur_type) {
				case 1: set Zeny,Zeny + .@bal; break;
				case 2: setd(""+.c_cash_var$[.@cur_ix]),getd(""+.c_cash_var$[.@cur_ix]) + .@bal; break;
				case 3: setd(""+.c_char_var$[.@cur_ix]),getd(""+.c_char_var$[.@cur_ix]) + .@bal; break;
				case 4: getitem .@cur_ix,.@bal; break;
			}
			query_sql "UPDATE `shop_owner` SET `balance` = '0' WHERE `acc_id` = '"+getcharid(3)+"'";
		}
		break;
		
		// Change Shop Name
		case 2:
		mes .n$;
		mes "Current Shop Name: \""+.@sh_n$+"\"";
		mes " ";
		mes "Please type the new Shop Name:";
		next;
		input(.@sh_nnew$);
		if(.@sh_nnew$ == .@sh_n$) {
			mes .n$;
			mes "It is the same, why bother trying to change it?";
			close;
		}
		mes .n$;
		mes "The Name of your shop has been changed successfully.";
		query_sql "UPDATE `shop_owner` SET `shop_name` = '"+.@sh_nnew$+"' WHERE `acc_id` = '"+getcharid(3)+"'";
		donpcevent "Shop Spot "+.@sh_id+"#"+.@sh_id+"::OnShopReLoad";
		break;
		
		// Change Currency
		case 3:
		mes .n$;
		mes "Current Currency: "+( (.@cur_type == 1)?"Zeny":( (.@cur_type == 2)?.c_cash_disp$[.@cur_ix]: ( (.@cur_type == 3)?.c_char_disp$[.@cur_ix]:getitemname(.@cur_ix) ) ) );
		mes " ";
		mes "- Use the number in front!!";
		mes " 1 = Zeny";
		mes " 2 = Cash Points or Kafra Points";
		mes " 3 = Custom Points";
		mes " Note: For 2 and 3 a list will be displayed after choosing.";
		mes " 4 = Items";
		next;
		if(input(.@cur_type,1,4) != 0) {
			mes .n$;
			mes "Invalid Currency, try again.";
			close;
		}
		mes .n$;
		if(.@cur_type == 2 || .@cur_type == 3) {
			mes "Please choose which points you want to use:";
			if(.@cur_type == 2) 
				for ( set .@c,0; .@c < getarraysize(.c_cash_disp$); set .@c,.@c + 1)
					set .@c_menu$,.@c_menu$ + "- "+.c_cash_disp$[.@c]+ ( (.c_cash_disp$[.@c] != "")?":":"");
			else if(.@cur_type == 3)
				for ( set .@c,0; .@c < getarraysize(.c_char_disp$); set .@c,.@c + 1)
					set .@c_menu$,.@c_menu$ + "- "+.c_char_disp$[.@c]+ ( (.c_char_disp$[.@c] != "")?":":"");
				
			set .@c_sel,select(.@c_menu$) - 1;
			next;
			mes .n$;
			mes "Chosen Points:";
			mes ( (.@cur_type == 2)?.c_cash_disp$[.@c_sel]:.c_char_disp$[.@c_sel]);
			mes " ";
			mes "Is this correct?";
			if(select("- Yes:- No") - 1) close;
			next;
			mes .n$;
		} else if(.@cur_type == 4) {
			mes "Please type the Item ID of the Item you want to use as currency:";
			next;
			input(.@c_sel);
			mes .n$;
			if(getitemname(.@c_sel) == "" || getitemname(.@c_sel) == "null") {
				mes "I'm sorry, but you have put an invalid Item ID.";
				close;
			}
			mes "Chosen Item: "+getitemname(.@c_sel);
			mes " ";
			mes "Is this correct?";
			if(select("- Yes:- No") - 1) close;
			next;
			mes .n$;
		}
		mes "The Currency for your shop has been changed successfully.";
		query_sql "UPDATE `shop_owner` SET `cur_type` = '"+.@cur_type+"' , `cur_ix` = '"+.@c_sel+"' WHERE `acc_id` = '"+getcharid(3)+"'";
		break;
		
		// Add Item
		case 4:
		mes .n$;
		getinventorylist;
		for ( set .@l,0; .@l < @inventorylist_count; set .@l,.@l + 1) {
			// Item is an equipment, skip it
			if(getiteminfo(@inventorylist_id[.@l],2) == 4 || getiteminfo(@inventorylist_id[.@l],2) == 5) continue;
			setarray .@shop_id[.@a],@inventorylist_id[.@l];
			setarray .@shop_amount[.@a],@inventorylist_amount[.@l];
			set .@a,.@a + 1;

			set .@shop_m$,.@shop_m$ + "> "+@inventorylist_amount[.@l]+"x "+ getitemname(@inventorylist_id[.@l]) + ( (@inventorylist_id[(.@l+1)] != 0)?":":"");
		}
		
		mes "Please choose the Item you want to add from the list, but remember I can't add Equipment to the Shop,";
		mes "as well as that I can only put one of the same item in the shop.";
		next;
		set .@c,select(.@shop_m$) - 1;
		
		if(query_sql("SELECT `cont_id` FROM `shop_content` WHERE `shop_id` = '"+.@sh_id+"' AND `cont_id` = '"+.@shop_id[.@c]+"'",.@i_id) != 0 ) {
			mes .n$;
			mes "I'm sorry, but the ID given is already in the Shop, please try again.";
			close;
		}
		mes .n$;
		mes "Now type the amount you want to put in the shop.";
		mes "Maximum: "+.item_am_max;
		mes "But remember you need to have the amount in your inventory, since I will put it in the shop.";
		next;
		if(input(.@i_am,1,.item_am_max) != 0) {
			mes .n$;
			mes "I'm sorry, either you put an amount less than 1 or higher than "+.item_am_max+".";
			close;
		}
		if(.@i_am > .@shop_amount[.@c]) {
			mes .n$;
			mes "I'm sorry, but you don't have enough of the selected Item with you.";
			close;
		}
		mes .n$;
		mes "Last would be the price per Item:";
		mes "Currency: "+( (.@cur_type == 1)?"Zeny":( (.@cur_type == 2)?.c_cash_disp$[.@cur_ix]: ( (.@cur_type == 3)?.c_char_disp$[.@cur_ix]:getitemname(.@cur_ix)) ) );
		mes "Maximum: "+( (.@cur_type == 1)?.zeny_pr_max:( (.@cur_type == 2 || .@cur_type == 3)?.var_pr_max:item_pr_max) );
		next;
		input(.@pr_am);
		switch(.@cur_type) {
		
			case 1: if(.@pr_am < 1 || .@pr_am > .zeny_pr_max) set .@p,1; break;
			case 2: 
			case 3: 
			if(.@pr_am < 1 || .@pr_am > .var_pr_max) set .@p,1;	break;
			
			case 4: if(.@pr_am < 1 || .@pr_am > .item_pr_max) set .@p,1; break;
		}
		mes .n$;
		if(.@p) {
			mes "I'm sorry, either you put an amount less than 1 or too high.";
			close;
		}
		mes "Result:";
		mes "Item: "+ getitemname(.@shop_id[.@c]);
		mes "Amount: "+.@i_am;
		mes "Price: "+.@pr_am;
		mes " ";
		mes "Is this correct?";
		if(select("- Yes:- No") - 1) close;
		next;
		mes .n$;
		mes "The Item has been added succesfully.";
		query_sql "INSERT INTO `shop_content` ( `shop_id` , `cont_id` , `cont_stock` , `cont_price` ) VALUES ( '"+.@sh_id+"' , '"+.@shop_id[.@c]+"' , '"+.@i_am+"' , '"+.@pr_am+"')";
		delitem .@shop_id[.@c],.@i_am;
		break;
		
		// Edit Item
		case 5:
		mes .n$;
		query_sql "SELECT `cont_id` , `cont_price` , `cont_stock` FROM `shop_content` WHERE `shop_id` = '"+.@sh_id+"'",.@c_id,.@c_price,.@c_am;
		if(getarraysize(.@c_id) < 1) {
			mes "I'm sorry, but there is no Item to edit. Add one first.";
			close;
		}
		mes "Which Item do you want to edit?";
		mes "Item Name (Price) [Stock]:";
		next;
		for( set .@l,0; .@l < getarraysize(.@c_id); set .@l,.@l + 1) 
			set .@ec_menu$,.@ec_menu$ + "- "+ getitemname(.@c_id[.@l])+" ("+.@c_price[.@l]+") ["+.@c_am[.@l]+"]"+ ( (.@c_id[.@l+1] == 0)?"":":");
			
		set .@c,select(.@ec_menu$) - 1;
		
		mes .n$;
		mes "Chosen Item:";
		mes getitemname(.@c_id[.@c]);
		mes " ";
		mes "Is this correct?";
		if(select("- Yes:- No") - 1) close;
		next;
		mes .n$;
		mes "What do you want to edit?";
		next;
		switch(select("- Item Price:- Stock Amount:- Leave")) {
			
			case 1:
			mes .n$;
			mes "Please type the new Price:";
			mes "Currency: "+( (.@cur_type == 1)?"Zeny":( (.@cur_type == 2)?.c_cash_disp$[.@cur_ix]: ( (.@cur_type == 3)?.c_char_disp$[.@cur_ix]:getitemname(.@cur_ix) ) ) );
			mes "Maximum: "+( (.@cur_type == 1)?.zeny_pr_max+" Zeny":( (.@cur_type == 2)?.var_pr_max+" "+.c_cash_disp$[.@cur_ix]: ( (.@cur_type == 3)?.c_char_disp$[.@cur_ix]+" "+.var_pr_max:item_pr_max ) ) );
			next;
			input(.@pr_new);
			switch(.@cur_type) {
			
				case 1: if(.@pr_new < 1 || .@pr_new > .zeny_pr_max) set .@p,1; break;
				case 2: 
				case 3: 
				if(.@pr_new < 1 || .@pr_new > .var_pr_max) set .@p,1;	break;
				
				case 4: if(.@pr_new < 1 || .@pr_new > .item_pr_max) set .@p,1; break;
			}
			mes .n$;
			if(.@p) {
				mes "I'm sorry, either you put an amount less than 1 or too high.";
				close;
			}
			mes "New Price: "+.@pr_new;
			mes "Is this correct?";
			if(select("- Yes:- No") - 1) close;
			next;
			mes .n$;
			mes "Item Price has been changed succesfully.";
			query_sql "UPDATE `shop_content` SET `cont_price` = '"+.@pr_new+"' WHERE `shop_id` = '"+.@sh_id+"' AND `cont_id` = '"+.@c_id[.@c]+"' AND `cont_stock` = '"+.@c_am[.@c]+"' AND `cont_price` = '"+.@c_price[.@c]+"'";
			close;
			
			case 2:
			mes .n$;
			mes "Please type the new Total Stock Amount:";
			mes "Note: This is not the amount which will be added!";
			mes "Maximum: "+.item_am_max;
			mes "But remember you need to have the amount in your inventory, since I will put it in the shop.";
			next;
			input(.@i_am);
			if((.@i_am+.@c_am[.@c]) < 1 || (.@i_am+.@c_am[.@c]) > .item_am_max) {
				mes .n$;
				mes "I'm sorry, either you put an amount less than 1 or the new Stock Amount would be higher than "+.item_am_max+".";
				close;
			}
			if(.@i_am > countitem(.@c_id[.@c])) {
				mes .n$;
				mes "I'm sorry, but you don't have enough of the selected Item with you.";
				close;
			}
			mes .n$;
			mes "New Stock Amount: "+(.@i_am+.@c_am[.@c]);
			mes "Is this correct?";
			if(select("- Yes:- No") - 1) close;
			next;
			mes .n$;
			mes "Item Stock Amount has been changed succesfully.";
			query_sql "UPDATE `shop_content` SET `cont_stock` = `cont_stock` + '"+.@i_am+"' WHERE `shop_id` = '"+.@sh_id+"' AND `cont_id` = '"+.@c_id[.@c]+"' AND `cont_stock` = '"+.@c_am[.@c]+"' AND `cont_price` = '"+.@c_price[.@c]+"'";
			delitem .@c_id[.@c],.@i_am;
			close;			
			
			case 3:
			break;
		}
		break;
		
		// Delete Item
		case 6:
		mes .n$;
		query_sql "SELECT `cont_id` , `cont_price` , `cont_stock` FROM `shop_content` WHERE `shop_id` = '"+.@sh_id+"'",.@c_id,.@c_price,.@c_am;
		if(getarraysize(.@c_id) < 1) {
			mes "I'm sorry, but there is no Item to delete. Add one first.";
			close;
		}
		mes "Which Item do you want to delete?";
		mes "Item Name (Price) [Stock]:";
		next;
		for( set .@l,0; .@l < getarraysize(.@c_id); set .@l,.@l + 1) 
			set .@ec_menu$,.@ec_menu$ + "- "+ getitemname(.@c_id[.@l])+" ("+.@c_price[.@l]+") ["+.@c_am[.@l]+"]"+ ( (.@c_id[.@l+1] == 0)?"":":");
			
		set .@c,select(.@ec_menu$) - 1;
		
		mes .n$;
		mes "Chosen Item:";
		mes getitemname(.@c_id[.@c]);
		mes " ";
		mes "Is this correct?";
		if(select("- Yes:- No") - 1) close;
		next;
		mes .n$;
		mes "The Item "+getitemname(.@c_id[.@c])+" has been deleted successfully.";
		query_sql "DELETE FROM `shop_content` WHERE `shop_id` = '"+.@sh_id+"' AND `cont_id` = '"+.@c_id[.@c]+"' AND `cont_stock` = '"+.@c_am[.@c]+"' AND `cont_price` = '"+.@c_price[.@c]+"'";
		getitem .@c_id[.@c],.@c_am[.@c];
		break;
		
		// Leave
		case 7:
		break;
	}
	break;
	
	case 4:
	mes .n$;
	mes "Do you want to be warped to the Shop Area?";
	if(select("- Yes:- No") - 1) break;
	close2;
	warp .shop_map$,.shop_x,.shop_y;
	end;
	
	case 5:
	break;
}
next;
mes .n$;
mes "Okay, see ya next time.";
close;

OnGMAccess:
mes .n$;
mes "Hello GM!";
mes "Currently I can only offer you to increase your amount of the currency you want!";
mes "- Use the number in front!!";
mes " 1 = Zeny";
mes " 2 = Cash Points or Kafra Points";
mes " 3 = Custom Points";
mes " Note: For 2 and 3 a list will be displayed after choosing.";
mes " 4 = Items";
mes " ";
next;
if(input(.@cur_type,1,4) != 0) {
	mes .n$;
	mes "Invalid Currency";
	close;
}
mes .n$;
if(.@cur_type == 2 || .@cur_type == 3) {
	mes "Please choose which points you want to use:";
	if(.@cur_type == 2) 
		for ( set .@c,0; .@c < getarraysize(.c_cash_disp$); set .@c,.@c + 1)
			set .@c_menu$,.@c_menu$ + "- "+.c_cash_disp$[.@c]+ ( (.c_cash_disp$[.@c] != "")?":":"");
	else if(.@cur_type == 3)
		for ( set .@c,0; .@c < getarraysize(.c_char_disp$); set .@c,.@c + 1)
			set .@c_menu$,.@c_menu$ + "- "+.c_char_disp$[.@c]+ ( (.c_char_disp$[.@c] != "")?":":"");
		
	set .@c_sel,select(.@c_menu$) - 1;
	next;
	mes .n$;
	mes "Chosen Points:";
	mes ( (.@cur_type == 2)?.c_cash_disp$[.@c_sel]:.c_char_disp$[.@c_sel]);
	mes "Your current Balance: "+( (.@cur_type == 2)?getd(""+.c_cash_var$[.@c_sel]):getd(""+.c_char_var$[.@c_sel]) );
	mes " ";
	mes "Is this correct?";
	if(select("- Yes:- No") - 1) close;
	
} else if(.@cur_type == 4) {
	mes "Please type the Item ID of the Item you want to use as currency:";
	next;
	input(.@c_sel);
	mes .n$;
	if(getitemname(.@c_sel) == "" || getitemname(.@c_sel) == "null") {
		mes "I'm sorry, but you have put an invalid Item ID.";
		close;
	}
	mes "Chosen Item: "+getitemname(.@c_sel);
	mes "You have "+countitem(.@c_sel)+"x "+getitemname(.@c_sel);
	mes " ";
	mes "Is this correct?";
	if(select("- Yes:- No") - 1) close;
}
next;
mes .n$;
mes "Now please chose the amount:";
mes "Maximum: "+( (.@cur_type == 1)?.zeny_pr_max:( (.@cur_type == 2 || .@cur_type == 3)?.var_pr_max:item_pr_max) );
input(.@cur_am);
switch(.@cur_type) {
	
	case 1: if(.@cur_am < 1 || .@cur_am > .zeny_pr_max) set .@p,1; break;
	case 2: 
	case 3: 
	if(.@cur_am < 1 || .@cur_am > .var_pr_max) set .@p,1;	break;
	case 4: if(.@cur_am < 1 || .@cur_am > .item_pr_max) set .@p,1; break;
}
next;
mes .n$;
if(.@p) {
	mes "I'm sorry, either you put an amount less than 1 or too high.";
	close;
}
mes "You have succesfully increased the chosen currency.";
switch(.@cur_type) {
	case 1: set Zeny,Zeny + .@cur_am; break;
	case 2: setd(""+getvariableofnpc(.c_cash_var$[.@c_sel],"Shop Rental")),getd(""+getvariableofnpc(.c_cash_var$[.@c_sel],"Shop Rental")) + .@cur_am; break;
	case 3: setd(""+getvariableofnpc(.c_char_var$[.@c_sel],"Shop Rental")),getd(""+getvariableofnpc(.c_char_var$[.@c_sel],"Shop Rental")) + .@cur_am; break;
	case 4: getitem .@c_sel,.@cur_am; break;
}
close;

OnInit:
set $@Sh_Sys,0;
set .n$,"["+strnpcinfo(0)+"]";
if(query_sql("SHOW TABLES LIKE 'shop_owner'",.@sh_tbl) == -1) // Checking if SQL Tables are installed
	end;
// ======= Shop Area =======
// Used to be warped to the Shop Area
set .shop_map$,"prontera";
set .shop_x,237;
set .shop_y,71;
// ======= Rental Settings =======
// > Rental Price Type
// 1 = Zeny
// 2 = Cash Points
// 3 = Item (like Coins)
set .rent_type,2;
// > Item ID for Type 3 (Item)
if(.rent_type == 3)
	set .rent_item,512; // Item ID
// > Rental Price
set .rent_pr,5;

// ======= Maximum Item Amount ======
set .item_am_max,10000;

// ======= Currency Settings =======
// Currency Maximum Settings
set .zeny_pr_max,1000000000; // 1,000,000,000 Zeny
set .var_pr_max,10000; // Max 10k Variable Points (EACH Variable, not total)
set .item_pr_max,10000; // Max 10k Item Price

// Variable Currency Settings
// - _var$ = Variable Name
// - _disp$ = Display Name of the Variable
// > Account Points: like #CASHPOINTS
setarray .c_cash_var$[0],"#CASHPOINTS","#KAFRAPOINTS";
setarray .c_cash_disp$[0],"Cash Points","Kafra Points";
// > Character Variables
setarray .c_char_var$[0],"EventPoints1","EventPoints2","EventPoints3","PvPPoints","WoEPoints";
setarray .c_char_disp$[0],"Event Points 1","Event Points 2","Event Points 3","PvP Points","WoE Points";

// ======= Shop Spot Settings =======
// Spot Unlocking
// = Each x Item sold, a new Spot will be unlocked to rent,
// this can be disabled by putting this to 0.
// > Example Values:
// = Low Rate: 100
// = Mid Rate: 500
// = High Rate: 2000
// = Super High Rate: 10000
// > Note: Depends on Drop Rate of items how much can be dropped and how easy/hard it can be obtained.
set .sh_sales,100;
// Total Shop Spots
set .sh_sp_total,20;
// ======= Shop Deletion Timer =====
// Values in seconds:
// > 1 Day = 86400
// > 1 Week = 604800
// > 1 Month = 2592000
set .time_ct,300;
// Custom atcommand: @pshop_gm
bindatcmd "pshop_gm",strnpcinfo(1)+"::OnGMAccess",99,99;
set $@Sh_Sys,1;

while(set(.@s,.@s + 1) <= .sh_sp_total) {
	disablenpc "Shop Spot "+.@s+"#"+.@s;
	sleep 100;
}

// Every 12 hours > Automatic Spot unlocking
// And checking for empty Shops by checking the timer
OnClock0000:
query_sql "SELECT `shop_id` , `timer` FROM `shop_owner`",.@sh_id,.@timer;
for ( set .@s,0; .@s < getarraysize(.@sh_id); set .@s,.@s + 1) {
	query_sql "SELECT `cont_id` , `cont_stock` FROM `shop_content` WHERE `shop_id` = '"+.@sh_id[.@s]+"'",.@c_id,.@c_am;
	for ( set .@c,0; .@c < getarraysize(.@c_am); set .@c,.@c + 1) 
		if(!.@c_am[.@c]) set .@e,.@e + 1;
	
	debugmes ".@e = "+.@e+", getarraysize(.@c_id): "+getarraysize(.@c_id);
	if(.@e == getarraysize(.@c_id))
		if(!.@timer[.@s])
			query_sql "UPDATE `shop_owner` SET `timer` = '"+gettimetick(2) + .time_ct+"' WHERE `shop_id` = '"+.@sh_id[.@s]+"'";
		else if(gettimetick(2) >= .@timer[.@s]) {
			query_sql "DELETE FROM `shop_content` WHERE `shop_id` = '"+.@sh_id[.@s]+"'";
			query_sql "DELETE FROM `shop_owner` WHERE `shop_id` = '"+.@sh_id[.@s]+"'";
		} else if(.@e != getarraysize(.@c_id))
			query_sql "UPDATE `shop_owner` SET `timer` = '0' WHERE `shop_id` = '"+.@sh_id[.@s]+"'";
}

OnClock1200:
set .@s,0;
if(!$@Sh_Sys) end;
if(!.sh_sales)
	while( set(.@s,.@s + 1) <= .sh_sp_total) {
		donpcevent "Shop Spot "+.@s+"#"+.@s+"::OnShopLoad";
		sleep 100;
	}
else {
	query_sql "SELECT SUM( `sales` ) FROM `shop_owner`",.@sales;
	if( set(.sh_spot,.@sales/.sh_sales) < 1) 
		set .sh_spot,1; // Spots Quantity

	while( set(.@s,.@s + 1) <= .sh_spot) {
		if(.@s == .sh_sp_total) break;
		donpcevent "Shop Spot "+.@s+"#"+.@s+"::OnShopLoad";
		sleep 100;
	}
}
end;

OnPCLoginEvent:
query_sql "SELECT `shop_id` , `timer` FROM `shop_owner` WHERE `acc_id` = '"+getcharid(3)+"'",.@sh_id,.@timer;
if(!.@sh_id) end;
query_sql "SELECT `cont_id` , `cont_stock` FROM `shop_content` WHERE `shop_id` = '"+.@sh_id+"'",.@c_id,.@c_am;
if(!getarraysize(.@c_am)) {
	if(!.@timer)
		query_sql "UPDATE `shop_owner` SET `timer` = '"+gettimetick(2) + .time_ct+"' WHERE `acc_id` = '"+getcharid(3)+"'";
	else if(gettimetick(2) >= .@timer) {
		dispbottom .n$+": Your Shop is getting deleted now.";
		query_sql "SELECT `balance` , `cur_type` , `cur_ix` FROM `shop_owner` WHERE `acc_id` = '"+getcharid(3)+"'",.@bal,.@cur_type,.@cur_ix;
		if(.@bal) {
			dispbottom .n$+": Your Balance is getting transfered: "+.@bal+"x "+ ( (.@cur_type == 1)?"Zeny":( (.@cur_type == 2)?.c_cash_disp$[.@cur_ix]: ( (.@cur_type == 3)?.c_char_disp$[.@cur_ix]:getitemname(.@cur_ix) ) ) );
			next;
			switch(.@cur_type) {
				case 1: set Zeny,Zeny + .@bal; break;
				case 2: setd(""+.c_cash_var$[.@cur_ix]),getd(""+.c_cash_var$[.@cur_ix]) + .@bal; break;
				case 3: setd(""+.c_char_var$[.@cur_ix]),getd(""+.c_char_var$[.@cur_ix]) + .@bal; break;
				case 4: getitem .@cur_ix,.@bal; break;
			}
		}
		query_sql "DELETE FROM `shop_content` WHERE `shop_id` = '"+.@sh_id+"'";
		query_sql "DELETE FROM `shop_owner` WHERE `acc_id` = '"+getcharid(3)+"'";
		end;
	}
	set .@time_cur,gettimetick(2);
	dispbottom .n$+": Your Shop Stock is currently empty, you have "+(.@time_cur-.@timer)/24+" Hours, "+(.@time_cur-.@timer)/24/60+" Minutes and "+(.@time_cur-.@timer)/24/60/60+" Seconds left until your shop gets removed.";
	end;
}
if(.@timer) 
	query_sql "UPDATE `shop_owner` SET `timer` = '0' WHERE `acc_id` = '"+getcharid(3)+"'";
	
for ( set .@l,0; .@l < getarraysize(.@c_id); set .@l,.@l + 1)
	dispbottom "> "+.@c_am[.@l]+"x "+getitemname(.@c_id[.@l]);
end;
}
// Shop Spot Template
-	script	Shop Spot 0#0::shop_rent	-1,{

query_sql "SELECT `char_name` , `cur_type` , `cur_ix` FROM `shop_owner` WHERE `shop_id` = '"+strnpcinfo(2)+"'",.@sh_char$,@cur_type,@cur_ix;
if(.sh_n$[0] == "") {
	mes "[Shop Spot#"+strnpcinfo(2)+"]";
	mes "This is currently a free shop spot.";
	mes "You can rent an spot for your own personal shop.";
	close;
}
mes "[ === "+.sh_n$+" === ]";
mes "Owner: "+.@sh_char$;
mes "Currency: "+ ( (@cur_type == 1)?"Zeny":( (@cur_type == 2)?getvariableofnpc(.c_cash_disp$[@cur_ix],"Shop Rental"): ( (@cur_type == 3)?getvariableofnpc(.c_char_disp$[@cur_ix],"Shop Rental"):getitemname(@cur_ix) ) ) );
mes "Your Balance:";
switch(@cur_type) { // Preventing buildin_countitem error from using short-ifs
	case 1: mes Zeny+"x Zeny"; break;
	case 2: mes getd(""+getvariableofnpc(.c_cash_var$[@cur_ix],"Shop Rental"))+"x "+getvariableofnpc(.c_cash_disp$[@cur_ix],"Shop Rental"); break;
	case 3: mes getd(""+getvariableofnpc(.c_char_var$[@cur_ix],"Shop Rental"))+"x "+getvariableofnpc(.c_char_disp$[@cur_ix],"Shop Rental"); break;
	case 4: mes countitem(@cur_ix)+"x "+getitemname(@cur_ix); break;
}
query_sql "SELECT `cont_id` , `cont_price` , `cont_stock` FROM `shop_content` WHERE `shop_id` = '"+strnpcinfo(2)+"'",@cont_id,@cont_pr,@cont_am;
if(!getarraysize(@cont_id) || !getarraysize(@cont_am)) {
	mes " ";
	mes "But currently there are no items in stock yet.";
	mes "Please come back later.";
	close;
}
next;
if(select("- Open Shop:- Leave") - 1) close;
close2;
set @cost,0;
set @total,0;
deletearray @bought_nameid[0],128;
deletearray @bought_quantity[0],128;

// Reseting the shop
npcshopitem "PlayerShop",512,1,1;
npcshopdelitem "PlayerShop",512;

for ( set .@b,0; .@b <= getarraysize(@cont_id); set .@b,.@b + 1) {
	if(!@cont_am[.@b]) continue;
	npcshopadditem "PlayerShop",@cont_id[.@b],@cont_pr[.@b],@cont_am[.@b];
}
callshop "PlayerShop",1;
npcshopattach "PlayerShop";
end;

OnBuyItem:
for ( set .@b,0; .@b < getarraysize(@bought_nameid); set .@b,.@b + 1) {
	for ( set .@l,0; .@l < getarraysize(@cont_id); set .@l,.@l + 1) {
		if(@bought_nameid[.@b] == @cont_id[.@l]) {
			if(@bought_quantity[.@b] > @cont_am[.@l]) {
				dispbottom .sh_n$+": I'm sorry, but I don't have the wanted quantity of the item \""+getitemname(@cont_id[.@l])+"\".";
				end;
			}
			if(checkweight(@cont_id[.@l],@bought_quantity[.@b]) == 1) {
				set @cost,@cont_pr[.@l]*@bought_quantity[.@b];
				set @total,@total+@cost;
			} else {
				dispbottom .sh_n$+": You would be overweight, please remove some items or buy less.";
				end;
			}
		}
	}
}
switch(@cur_type) {
	case 1:
	if(@total > Zeny) { 
		dispbottom .sh_n$+": You don't have enough Zeny, you need "+@total+"!";
		end;
	}
	set Zeny,Zeny - @total;
	dispbottom .sh_n$+": You have paied "+@total+"x Zeny";
	break;
	
	case 2:
	if(@total > getd(""+getvariableofnpc(.c_cash_var$[@cur_ix],"Shop Rental")) ) {
		dispbottom .sh_n$+": You don't have enough "+getvariableofnpc(.c_cash_disp$[@cur_ix],"Shop Rental")+", you need "+@total+"!";
		end; 
	}
	setd(""+getvariableofnpc(.c_cash_var$[@cur_ix],"Shop Rental")),getd(""+getvariableofnpc(.c_cash_var$[@cur_ix],"Shop Rental")) - @total;
	dispbottom .sh_n$+": You have paied "+@total+"x "+getvariableofnpc(.c_cash_disp$[@cur_ix],"Shop Rental");
	break;
	
	case 3:
	if(@total > getd(""+getvariableofnpc(.c_char_var$[@cur_ix],"Shop Rental")) ) {
		dispbottom .sh_n$+": You don't have enough "+getvariableofnpc(.c_char_disp$[@cur_ix],"Shop Rental")+", you need "+@total+"!";
		end;
	}
	setd(""+getvariableofnpc(.c_char_var$[@cur_ix],"Shop Rental")),getd(""+getvariableofnpc(.c_char_var$[@cur_ix],"Shop Rental")) - @total;
	dispbottom .sh_n$+": You have paied "+@total+"x "+getvariableofnpc(.c_char_disp$[@cur_ix],"Shop Rental");
	break;
	
	case 4:
	if(@total > countitem(@cur_ix)) {
		dispbottom .sh_$+": You don't have enough "+getitemname(@cur_ix)+", you need "+@total+"!";
		end;
	}
	delitem @cur_ix,@total;
	dispbottom .sh_n$+": You have paied "+@total+"x "+getitemname(@cur_ix);
	break;
}
for ( set .@s,0; .@s < getarraysize(@bought_nameid); set .@s,.@s + 1) {
	getitem @bought_nameid[.@s],@bought_quantity[.@s];
	query_sql "UPDATE `shop_content` SET `cont_stock` = `cont_stock` - '"+@bought_quantity[.@s]+"' WHERE `shop_id` = '"+strnpcinfo(2)+"' AND `cont_id` = '"+@bought_nameid[.@s]+"'";
	dispbottom .sh_n$+": You have succesfully purchased "+@bought_quantity[.@s]+"x "+getitemname(@bought_nameid[.@s])+".";
	query_sql "UPDATE `shop_owner` SET `sales` = `sales` + '"+@bought_quantity[.@s]+"' WHERE `shop_id` = '"+strnpcinfo(2)+"'";
}
query_sql "UPDATE `shop_owner` SET `balance` = `balance` + '"+@total+"' WHERE `shop_id` = '"+strnpcinfo(2)+"'";
end;

OnShopLoad:
if(.Enable == 0) {
	enablenpc strnpcinfo(0);
	set .Enable,1;
}
OnShopReLoad:
delwaitingroom;
query_sql "SELECT `shop_name` FROM `shop_owner` WHERE `shop_id` = '"+strnpcinfo(2)+"'",.sh_n$;
waitingroom (.sh_n$ != "")?.sh_n$:"Empty Spot",0;
end;
}

// Shop Spots - Duplicates
// > Only change the coordinates!!!
// If you want to add more, remember to change the value at .sh_sp_total
prontera,231,82,0	duplicate(shop_rent)	Shop Spot 1#1	837
prontera,231,80,0	duplicate(shop_rent)	Shop Spot 2#2	837
prontera,231,78,0	duplicate(shop_rent)	Shop Spot 3#3	837 
prontera,231,76,0	duplicate(shop_rent)	Shop Spot 4#4	837 
prontera,231,74,0	duplicate(shop_rent)	Shop Spot 5#5	837 
prontera,231,72,0	duplicate(shop_rent)	Shop Spot 6#6	837 
prontera,231,70,0	duplicate(shop_rent)	Shop Spot 7#7	837 
prontera,231,68,0	duplicate(shop_rent)	Shop Spot 8#8	837 
prontera,231,66,0	duplicate(shop_rent)	Shop Spot 9#9	837 
prontera,231,64,0	duplicate(shop_rent)	Shop Spot 10#10	837 
prontera,244,82,0	duplicate(shop_rent)	Shop Spot 11#11	837 
prontera,244,80,0	duplicate(shop_rent)	Shop Spot 12#12	837 
prontera,244,78,0	duplicate(shop_rent)	Shop Spot 13#13	837 
prontera,244,76,0	duplicate(shop_rent)	Shop Spot 14#14	837 
prontera,244,74,0	duplicate(shop_rent)	Shop Spot 15#15	837 
prontera,244,72,0	duplicate(shop_rent)	Shop Spot 16#16	837 
prontera,244,70,0	duplicate(shop_rent)	Shop Spot 17#17	837 
prontera,244,68,0	duplicate(shop_rent)	Shop Spot 18#18	837 
prontera,244,66,0	duplicate(shop_rent)	Shop Spot 19#19	837 
prontera,244,64,0	duplicate(shop_rent)	Shop Spot 20#20	837 

// Shop
-	marketshop	PlayerShop	-1,512:-1:1